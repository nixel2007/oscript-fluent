#Использовать lambdas

Перем Действие;
Перем ВозвращаемоеЗначение;

Функция Принять(Элемент) Экспорт

	Если Действие.Выполнить(Элемент) Тогда
		Возврат Новый Структура("Действие, Значение", ДействияКонвейера.ВернутьЗначение, ВозвращаемоеЗначение);
	КонецЕсли;

	Возврат Новый Структура("Действие", ДействияКонвейера.Бездействие);

КонецФункции

Функция Барьерный() Экспорт
	Возврат Ложь;
КонецФункции

Процедура Деструктор() Экспорт
	
	ОсвободитьОбъект(Действие);

	Действие = Неопределено;

КонецПроцедуры

Процедура Инвертировать(ТолькоЗначение = Ложь) Экспорт
	
	Если Не ТолькоЗначение Тогда

		Действие = Лямбда.Выражение("Элемент -> Возврат Не Действие.Выполнить(Элемент)")
			.Контекст(Новый Структура("Действие", Действие))
			.ВДействие();

	КонецЕсли;

	ВозвращаемоеЗначение = Не ВозвращаемоеЗначение;

КонецПроцедуры

Процедура ПриСозданииОбъекта(ФункцияФильтрации, ДополнительныеПараметры, Отладка)

	Действие = ПроцессорыКоллекцийСлужебный.ДействиеИзПараметров(
		ФункцияФильтрации,
		ФункциональныеИнтерфейсы.УниФункция(),
		ДополнительныеПараметры,
		Отладка
	);

	ВозвращаемоеЗначение = Истина;

КонецПроцедуры
