#Использовать ".."

Функция ОбработчикФильтрации(Элемент) Экспорт
	Возврат Элемент > 3;
КонецФункции

Функция ОбработчикОбработки(Элемент) Экспорт
	Результат = Элемент + 1;
	Сообщить("Я - операция map, и это мое послание миру: элемент " + Элемент + " превратился в " + Результат);
	Возврат Результат;
КонецФункции

Функция ОбработчикСокращения(Результат, Элемент) Экспорт
	Возврат Результат + Элемент;
КонецФункции

ФункцияФильтрации = Новый Действие(ЭтотОбъект, "ОбработчикФильтрации");
ФункцияОбработки = Новый Действие(ЭтотОбъект, "ОбработчикОбработки");
ФункцияСокращения = Новый Действие(ЭтотОбъект, "ОбработчикСокращения");

Массив = Новый Массив;
Массив.Добавить(3);
Массив.Добавить(4);
Массив.Добавить(7);
Массив.Добавить(5);
Массив.Добавить(1);
Массив.Добавить(0);

ПроцессорКоллекций = Новый ПроцессорКоллекций;
ПроцессорКоллекций.УстановитьКоллекцию(Массив);

Результат = ПроцессорКоллекций
	.Пропустить(2)
	.Фильтровать(ФункцияФильтрации)
	.Первые(2)
	.Обработать(ФункцияОбработки)
	.Получить(Тип("Массив"));

Для Каждого Элемент Из Результат Цикл
	Сообщить(Элемент);
КонецЦикла;

ПроцессорКоллекций = ПроцессорыКоллекций.ИзКоллекции(Массив);
ПроцессорКоллекций
	.Сортировать()
	.ДляКаждого("Элемент -> Сообщить(Элемент)");

Массив = Новый Массив;
Массив.Добавить(4);
Массив.Добавить(2);
Массив.Добавить(2);
Массив.Добавить(3);
Массив.Добавить(3);
Массив.Добавить(3);

ПроцессорКоллекций = ПроцессорыКоллекций.ИзКоллекции(Массив);
ПроцессорКоллекций
	.Различные()
	.ДляКаждого("Элемент -> Сообщить(Элемент)");

ПроцессорКоллекций = ПроцессорыКоллекций.ИзКоллекции(Массив);
Результат = ПроцессорКоллекций
	.Обработать(ФункцияОбработки)
	.Сократить(ФункцияСокращения, 0);

Сообщить(Результат);

// Результат должен различаться
ПроцессорыКоллекций
	.ИзКоллекции(Массив)
	.Сортировать()
	.Первые(1)
	.ДляКаждого("Элемент -> Сообщить(Элемент)");
Сообщить(Массив[0]);

Строка = "ФЫВА";
ПроцессорыКоллекций
	.ИзСтроки(Строка)
	.Сортировать()
	.ДляКаждого("Элемент -> Сообщить(Элемент)");

Строка = "Я строка с пробелами";
ПроцессорыКоллекций
	.ИзСтроки(Строка, " ")
	.ДляКаждого("Элемент -> Сообщить(Элемент)");

Строка = "Я
|строка
|многострочная";

ПроцессорыКоллекций
	.ИзСтроки(Строка, Символы.ПС)
	.Сортировать()
	.ДляКаждого("Элемент -> Сообщить(Элемент)");

ПроцессорыКоллекций
	.ИзСтроки(Строка, Символы.ПС)
	.Фильтровать("Элемент -> СтрДлина(Элемент) > 1")
	.ДляКаждого("Элемент -> Сообщить(Элемент)");
