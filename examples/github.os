#Использовать ".."

Перем Таблица;

Процедура ДобавитьСтрокуВТаблицу(Имя, Фолловеры, Местоположение, Контрибьюции)
	
	Строка = Таблица.Добавить();
	
	Строка.Имя            = Имя;
	Строка.Фолловеры      = Фолловеры;
	Строка.Местоположение = Местоположение;
	Строка.Контрибьюции   = Контрибьюции;

КонецПроцедуры

Функция Обработчик_ФильтрацияПоМестоположению(Элемент) Экспорт
	Возврат Элемент.Местоположение = "Россия";
КонецФункции

Функция Обработчик_СортировкаПоФолловерам(Первый, Второй) Экспорт

	Если Первый.Фолловеры > Второй.Фолловеры Тогда
		Возврат 1;
	ИначеЕсли Второй.Фолловеры > Первый.Фолловеры Тогда
		Возврат -1;
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции

Функция Обработчик_СортировкаПоКонтрибьюциям(Первый, Второй) Экспорт

	Если Первый.Контрибьюции > Второй.Контрибьюции Тогда
		Возврат 1;
	ИначеЕсли Второй.Контрибьюции > Первый.Контрибьюции Тогда
		Возврат -1;
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции

ФильтрацияПоМестоположению = Новый Действие(ЭтотОбъект, "Обработчик_ФильтрацияПоМестоположению");
СортировкаПоФолловерам = Новый Действие(ЭтотОбъект, "Обработчик_СортировкаПоФолловерам");
СортировкаПоКонтрибьюциям = Новый Действие(ЭтотОбъект, "Обработчик_СортировкаПоКонтрибьюциям");

Таблица = Новый ТаблицаЗначений;
Таблица.Колонки.Добавить("Имя");
Таблица.Колонки.Добавить("Фолловеры");
Таблица.Колонки.Добавить("Местоположение");
Таблица.Колонки.Добавить("Контрибьюции");

ДобавитьСтрокуВТаблицу("Иванов", 10, "Новая Зеландия", 45);
ДобавитьСтрокуВТаблицу("Петров", 0, "Россия", 50);
ДобавитьСтрокуВТаблицу("Сидоров", 15, "Россия", 12);
ДобавитьСтрокуВТаблицу("Туполев", 99, "Россия", 44);
ДобавитьСтрокуВТаблицу("Миль", 23, "Нидерланды", 31);
ДобавитьСтрокуВТаблицу("Сухой", 3, "Россия", 123);
ДобавитьСтрокуВТаблицу("Лавочкин", 10, "Россия", 68);
ДобавитьСтрокуВТаблицу("Яковлев", 12, "Россия", 99);

ПроцессорКоллекций = ПроцессорыКоллекций.ИзКоллекции(Таблица);
Результат = ПроцессорКоллекций
	.Фильтровать(ФильтрацияПоМестоположению)
	.Сортировать(СортировкаПоФолловерам)
	.Первые(5)
	.Сортировать(СортировкаПоКонтрибьюциям)
	.Первые(3)
	.Получить(Тип("ТаблицаЗначений"));

Для Каждого Элемент Из Результат Цикл
	Сообщить(Элемент.Имя);
КонецЦикла;

ПроцессорыКоллекций.ИзКоллекции(Таблица)
	.Фильтровать("Элемент -> Элемент.Местоположение = ""Россия""")
	.Сортировать("Первый, Второй -> 
		|	Если Первый.Фолловеры > Второй.Фолловеры Тогда
		|		Возврат 1;
		|	ИначеЕсли Второй.Фолловеры > Первый.Фолловеры Тогда
		|		Возврат -1;
		|	Иначе
		|		Возврат 0;
		|	КонецЕсли;"
	)
	.Первые(5)
	.Сортировать("Первый, Второй -> 
		|	Если Первый.Контрибьюции > Второй.Контрибьюции Тогда
		|		Возврат 1;
		|	ИначеЕсли Второй.Контрибьюции > Первый.Контрибьюции Тогда
		|		Возврат -1;
		|	Иначе
		|		Возврат 0;
		|	КонецЕсли;"
	)
	.Первые(3)
	.ДляКаждого("Элемент -> Сообщить(Элемент.Имя)");

// githubUsers
// 	.filter(_.location == 'Russia') 
//	.sort(_.followers)
// 	.take(1000)
// 	.sort(_.contributions)
// 	.take(256)
