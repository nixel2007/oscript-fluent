#Использовать lambdas
#Использовать notify
#Использовать asserts

Перем Действие;

Функция Принять(Коллекция) Экспорт

	Результат = Новый Массив;

	Для каждого Элемент Из Коллекция Цикл
		
		Процессор = Действие.Выполнить(Элемент);

		ДСО = СтрШаблон("Результат разворачивания для элемента %1 должен иметь тип ПроцессорКоллекций", Элемент);
		Ожидаем.Что(Процессор, ДСО).ИмеетТип("ПроцессорКоллекций");

		Процессор.ДляКаждого(
			"Элемент -> Коллекция.Добавить(Элемент)",
			Новый Структура("Коллекция", Результат)
		);

	КонецЦикла;

	Возврат Новый Структура("Действие, Коллекция", ДействияКонвейера.ЗаменитьКоллекцию, Результат);

КонецФункции

Функция Барьерный() Экспорт
	Возврат Истина;
КонецФункции

Процедура Деструктор() Экспорт
	
	ОсвободитьОбъект(Действие);

	Действие = Неопределено;

КонецПроцедуры

Процедура ПриСозданииОбъекта(ФункцияРазворачивания, ДополнительныеПараметры, Отладка)

	Действие = ПроцессорыКоллекцийСлужебный.ДействиеИзПараметров(
		ФункцияРазворачивания,
		ФункциональныеИнтерфейсы.УниФункция(),
		ДополнительныеПараметры,
		Отладка
	);

КонецПроцедуры
