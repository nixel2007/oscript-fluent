Перем Действие;
Перем НачальноеЗначение;

Функция Принять(Коллекция) Экспорт
	
	Результат = НачальноеЗначение;
	Для Каждого Элемент Из Коллекция Цикл
		Результат = Действие.Выполнить(Результат, Элемент);
	КонецЦикла;
	
	Возврат Новый Структура(
		"Действие, Значение",
		ДействияКонвейера.ВернутьЗначение,
		Результат
	);

КонецФункции

Функция Барьерный() Экспорт
	Возврат Истина;
КонецФункции

Процедура Деструктор() Экспорт
	
	ОсвободитьОбъект(Действие);
	Действие = Неопределено;

КонецПроцедуры

Процедура ПриСозданииОбъекта(ФункцияСокращения, ДополнительныеПараметры, пНачальноеЗначение, Отладка)
	
	Действие = ПроцессорыКоллекцийСлужебный.ДействиеИзПараметров(
		ФункцияСокращения,
		ФункциональныеИнтерфейсы.БиФункция(),
		ДополнительныеПараметры,
		Отладка,
		Истина
	);

	НачальноеЗначение = пНачальноеЗначение;

КонецПроцедуры
