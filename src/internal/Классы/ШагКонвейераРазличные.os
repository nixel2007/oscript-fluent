#Использовать lambdas

Перем ДействиеРавенство;
Перем ПринятыеЭлементы;

Функция Принять(Элемент) Экспорт

	Для каждого ПринятыйЭлемент Из ПринятыеЭлементы Цикл
		
		Если ДействиеРавенство.Выполнить(ПринятыйЭлемент, Элемент) Тогда
			Возврат Новый Структура("Действие", ДействияКонвейера.ПропуститьЭлемент);
		КонецЕсли;

	КонецЦикла;

	ПринятыеЭлементы.Добавить(Элемент);

	Возврат Новый Структура("Действие", ДействияКонвейера.Бездействие);

КонецФункции

Функция Барьерный() Экспорт
	Возврат Ложь;
КонецФункции

Процедура Деструктор() Экспорт
	
	ОсвободитьОбъект(ПринятыеЭлементы);
	ОсвободитьОбъект(ДействиеРавенство);

	ПринятыеЭлементы  = Неопределено;
	ДействиеРавенство = Неопределено;

КонецПроцедуры

Процедура ПриСозданииОбъекта(ФункцияРавенства, ДополнительныеПараметры, Отладка)
	
	ПринятыеЭлементы = Новый Массив();
	
	Если ФункцияРавенства = Неопределено Тогда
		ДействиеРавенство = ПроцессорыКоллекций.СтандартнаяФункцияРавенства();
	Иначе

		ДействиеРавенство = ПроцессорыКоллекцийСлужебный.ДействиеИзПараметров(
			ФункцияРавенства,
			ФункциональныеИнтерфейсы.БиФункция(),
			ДополнительныеПараметры,
			Отладка
		);

	КонецЕсли;

КонецПроцедуры
