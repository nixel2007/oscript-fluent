Перем ТипРезультата;
Перем КэшКолонок;

Функция Принять(Коллекция) Экспорт
	
	Результат = Новый(ТипРезультата);
	
	РезультатСодержитКолонки = Истина;
	Попытка
		Колонки = Результат.Колонки;
	Исключение
		РезультатСодержитКолонки = Ложь;
	КонецПопытки;
	
	Если РезультатСодержитКолонки Тогда
		
		Для Каждого Колонка Из КэшКолонок Цикл
			Результат.Колонки.Добавить(
				Колонка.Имя,
				Колонка.ТипЗначения,
				Колонка.Заголовок,
				Колонка.Ширина
			);
		КонецЦикла;
	
		Если Результат.Колонки.Количество() = 0 Тогда
			Результат.Колонки.Добавить("Значение");
		КонецЕсли;

	КонецЕсли;

	ЭлементСодержитКолонки = КэшКолонок.Количество() > 0;

	Для Каждого ЭлементКоллекции Из Коллекция Цикл
		
		Если РезультатСодержитКолонки И ЭлементСодержитКолонки Тогда
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), ЭлементКоллекции);
		ИначеЕсли РезультатСодержитКолонки Тогда
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Значение = ЭлементКоллекции;
		Иначе
			Результат.Добавить(ЭлементКоллекции);
		КонецЕсли;

	КонецЦикла;

	Возврат Новый Структура("Действие, Значение", ДействияКонвейера.ВернутьЗначение, Результат);

КонецФункции

Функция Барьерный() Экспорт
	Возврат Истина;
КонецФункции

Процедура Деструктор() Экспорт
	КэшКолонок = Неопределено;
КонецПроцедуры

Процедура ПриСозданииОбъекта(пТипРезультата, пКэшКолонок = Неопределено)
	
	ТипРезультата = пТипРезультата;

	Если пКэшКолонок = Неопределено Тогда
		КэшКолонок = Новый Массив;
	Иначе
		КэшКолонок = пКэшКолонок;
	КонецЕсли;

КонецПроцедуры
