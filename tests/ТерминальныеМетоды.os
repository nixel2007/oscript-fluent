#Использовать asserts
#Использовать notify
#Использовать ".."

Перем ВнешнийМассив;

Функция ПолучитьНачальныйМассив()
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(0);
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(3);
	Массив.Добавить(1);

	Возврат Массив;
КонецФункции

Функция ПолучитьПроцессорКоллекций()

	Возврат ПроцессорыКоллекций.ИзКоллекции(ПолучитьНачальныйМассив());

КонецФункции

Процедура ФункцияОбработкиСПроверкойДопПараметров(Результат, ДополнительныеПараметры) Экспорт
	Результат = ДополнительныеПараметры.ПроверяемыйПараметр;
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьПервый() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	Результат = ПроцессорКоллекций.ПолучитьПервый();
	Ожидаем.Что(Результат).ИмеетТип("Число").Равно(1);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВернутьМассив() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	Результат = ПроцессорКоллекций.ВМассив();
	Ожидаем.Что(Результат).ИмеетТип("Массив").ИмеетДлину(6);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВернутьСтроку() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	Результат = ПроцессорКоллекций.ВСтроку();
	Ожидаем.Что(Результат).ИмеетТип("Строка").ИмеетДлину(6);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьКоличество() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	Результат = ПроцессорКоллекций.Количество();
	Ожидаем.Что(Результат).Равно(6);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыполнитьДляКаждого() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВнешнийМассив", Новый Массив);
	ПроцессорКоллекций.ДляКаждого("ДополнительныеПараметры.ВнешнийМассив.Добавить(Элемент + 1)", ДополнительныеПараметры);
	Ожидаем.Что(ДополнительныеПараметры.ВнешнийМассив).ИмеетДлину(НачальныйМассив.Количество());
	Для сч = 0 По ДополнительныеПараметры.ВнешнийМассив.ВГраница() Цикл
		Ожидаем.Что(ДополнительныеПараметры.ВнешнийМассив[сч]).Равно(НачальныйМассив[сч] + 1);
	КонецЦикла;
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыполнитьДляКаждого_Лямбда() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВнешнийМассив", Новый Массив);
	ПроцессорКоллекций.ДляКаждого("Элемент -> ВнешнийМассив.Добавить(Элемент + 1)", ДополнительныеПараметры);
	Ожидаем.Что(ДополнительныеПараметры.ВнешнийМассив).ИмеетДлину(НачальныйМассив.Количество());
	Для сч = 0 По ДополнительныеПараметры.ВнешнийМассив.ВГраница() Цикл
		Ожидаем.Что(ДополнительныеПараметры.ВнешнийМассив[сч]).Равно(НачальныйМассив[сч] + 1);
	КонецЦикла;
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыполнитьДляКаждого_Захват() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();

	ВнешнийМассив = Новый Массив;

	ПроцессорКоллекций.ДляКаждого("Элемент -> ВнешнийМассив.Добавить(Элемент + 1)", ЭтотОбъект);
	Ожидаем.Что(ВнешнийМассив).ИмеетДлину(НачальныйМассив.Количество());
	Для сч = 0 По ВнешнийМассив.ВГраница() Цикл
		Ожидаем.Что(ВнешнийМассив[сч]).Равно(НачальныйМассив[сч] + 1);
	КонецЦикла;
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьМинимальныйЭлемент() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	Результат = ПроцессорКоллекций.Минимум();
	Ожидаем.Что(Результат).Равно(0);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьМаксимальныйЭлемент() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	Результат = ПроцессорКоллекций.Максимум();
	Ожидаем.Что(Результат).Равно(3);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыполнитьСокращение() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.Сократить("Результат = Результат + Элемент", 0);
	Ожидаем.Что(Результат).Равно(10);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ВыполнитьСокращение_Лямбда() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.Сократить("Результат, Элемент -> Результат + Элемент", 0);
	Ожидаем.Что(Результат).Равно(10);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьМассив() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();

	Результат = ПроцессорКоллекций.Получить(Тип("Массив"));
	Ожидаем.Что(Результат).ИмеетТип("Массив").ИмеетДлину(НачальныйМассив.Количество());

	Для сч = 0 По НачальныйМассив.ВГраница() Цикл
		Ожидаем.Что(НачальныйМассив[сч]).Равно(Результат[сч]);
	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьТаблицуЗначений() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();
	
	Результат = ПроцессорКоллекций.Получить(Тип("ТаблицаЗначений"));
	Ожидаем.Что(Результат).ИмеетТип("ТаблицаЗначений").ИмеетДлину(НачальныйМассив.Количество());
	
	Для сч = 0 По НачальныйМассив.ВГраница() Цикл
		Ожидаем.Что(НачальныйМассив[сч]).Равно(Результат[сч].Значение);
	КонецЦикла;
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПолучитьСписокЗначений() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();
	
	Результат = ПроцессорКоллекций.Получить(Тип("СписокЗначений"));
	Ожидаем.Что(Результат).ИмеетТип("СписокЗначений").ИмеетДлину(НачальныйМассив.Количество());
	
	Для сч = 0 По НачальныйМассив.ВГраница() Цикл
		Ожидаем.Что(НачальныйМассив[сч]).Равно(Результат[сч].Значение);
	КонецЦикла;
	
КонецПроцедуры

Процедура ТестДолжен_ПолучитьСтруктуру()
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();
	
	Результат = ПроцессорКоллекций.Получить(Тип("Структура"));
	Ожидаем.Что(Результат).ИмеетТип("Структура").ИмеетДлину(НачальныйМассив.Количество());
	
	Для сч = 0 По НачальныйМассив.ВГраница() Цикл
		Ожидаем.Что(НачальныйМассив[сч]).Равно(Результат[сч].Значение);
	КонецЦикла;
	
КонецПроцедуры

Процедура ТестДолжен_ПолучитьСоответствие()
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	НачальныйМассив = ПолучитьНачальныйМассив();
	
	Результат = ПроцессорКоллекций.Получить(Тип("Соответствие"));
	Ожидаем.Что(Результат).ИмеетТип("Соответствие").ИмеетДлину(НачальныйМассив.Количество());
	
	Для сч = 0 По НачальныйМассив.ВГраница() Цикл
		Ожидаем.Что(НачальныйМассив[сч]).Равно(Результат[сч].Значение);
	КонецЦикла;
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьЛюбойСоответствует() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.ЛюбойСоответствует("Результат = Элемент > 1");
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Результат = ПроцессорКоллекций.ЛюбойСоответствует("Результат = Элемент < 0");
	Ожидаем.Что(Результат).ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьЛюбойСоответствует_Лямбда() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.ЛюбойСоответствует("Элемент -> Элемент > 1");
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Результат = ПроцессорКоллекций.ЛюбойСоответствует("Элемент -> Элемент < 0");
	Ожидаем.Что(Результат).ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВсеСоответствуют() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.ВсеСоответствуют("Результат = Элемент >= 0");
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Результат = ПроцессорКоллекций.ВсеСоответствуют("Результат = Элемент < 2");
	Ожидаем.Что(Результат).ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВсеСоответствуют_Лямбда() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.ВсеСоответствуют("Элемент -> Элемент >= 0");
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Результат = ПроцессорКоллекций.ВсеСоответствуют("Элемент -> Элемент < 2");
	Ожидаем.Что(Результат).ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВсеНеСоответствуют() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.ВсеНеСоответствуют("Результат = Элемент < 0");
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Результат = ПроцессорКоллекций.ВсеНеСоответствуют("Результат = Элемент > 2");
	Ожидаем.Что(Результат).ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВсеНеСоответствуют_Лямбда() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	Результат = ПроцессорКоллекций.ВсеНеСоответствуют("Элемент -> Элемент < 0");
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Результат = ПроцессорКоллекций.ВсеНеСоответствуют("Элемент -> Элемент > 2");
	Ожидаем.Что(Результат).ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПробросДополнительныхПараметровВОповещениеДляКаждого() Экспорт
	ПроцессорКоллекций = ПолучитьПроцессорКоллекций();
	
	ДополнительныеПараметры = Новый Структура("ПроверяемыйПараметр");
	ФункцияОбработки = Новый ОписаниеОповещения(
		"ФункцияОбработкиСПроверкойДопПараметров",
		ЭтотОбъект,
		ДополнительныеПараметры
	);

	// Ожидаем, что все варианты не выкинут исключения
	ПроцессорКоллекций.ДляКаждого(ФункцияОбработки);
	ПроцессорКоллекций.ДляКаждого("Результат = ДополнительныеПараметры.ПроверяемыйПараметр;", ДополнительныеПараметры);

	ФункцияОбработки = Новый ОписаниеОповещения(
		"ФункцияОбработкиСПроверкойДопПараметров",
		ЭтотОбъект
	);
	ПроцессорКоллекций.ДляКаждого(ФункцияОбработки, ДополнительныеПараметры);
	
КонецПроцедуры
